shader_type canvas_item;

uniform float outline_thickness = 20.0;
uniform vec4 outline_color: source_color = vec4(1.0);
uniform bool is_outline_on = false;

uniform sampler2D overlay_tex: repeat_enable, filter_linear;
uniform vec4 mask_color;

bool is_inside_usquare(vec2 x) {
	return x == clamp(x, vec2(0.0), vec2(1.0));
}

vec4 blend(vec4 bottom, vec4 top) {
    float alpha = top.a + bottom.a * (1.0 - top.a);
    if (alpha < 0.0001) return vec4(0.0);
    vec3 color = mix(bottom.rgb * bottom.a, top.rgb, top.a) / alpha;
    return vec4(color, alpha);
}

void vertex() {
	VERTEX += (UV * 2.0 - 1.0) * outline_thickness;
}

void fragment() {
	vec2 uv = UV;
	vec4 texture_color = texture(TEXTURE, UV);
	
	vec2 texture_pixel_size = vec2(1.0) / (vec2(1.0) / TEXTURE_PIXEL_SIZE + vec2(outline_thickness * 2.0));
	uv = (uv - texture_pixel_size * outline_thickness) * TEXTURE_PIXEL_SIZE / texture_pixel_size;
	if (is_inside_usquare(uv)) {
		texture_color = texture(TEXTURE, uv);
		float mix_amount = texture_color.r;
		vec4 overlay_color = texture(overlay_tex, uv);
		vec4 grey_scale = mix(texture_color, overlay_color, mix_amount);
		texture_color = mix(grey_scale, mask_color, mix_amount * 0.5);
	} else {
		texture_color = vec4(0.0);
	}
		
	float alpha = 0.0;
	
	if(is_outline_on) {
		for (float y = 1.0; y <= outline_thickness; y++) {
			for (float x = 0.0; x <= y; x++) {
				if (length(vec2(x, y - 0.5)) > outline_thickness) break;
				float look_at_alpha;
				vec2 look_at_uv[8] = {
					uv + vec2(x, y) * TEXTURE_PIXEL_SIZE,
					uv + vec2(-x, y) * TEXTURE_PIXEL_SIZE,
					uv + vec2(x, -y) * TEXTURE_PIXEL_SIZE,
					uv + vec2(-x, -y) * TEXTURE_PIXEL_SIZE,
					uv + vec2(y, x) * TEXTURE_PIXEL_SIZE,
					uv + vec2(-y, x) * TEXTURE_PIXEL_SIZE,
					uv + vec2(y, -x) * TEXTURE_PIXEL_SIZE,
					uv + vec2(-y, -x) * TEXTURE_PIXEL_SIZE
				};
				for (int i = 0; i < 8; i++) {
					if (is_inside_usquare(look_at_uv[i])) {
						look_at_alpha = texture(TEXTURE, look_at_uv[i]).a;
						if (look_at_alpha > alpha) alpha = look_at_alpha;
						if (1.0 - alpha < 0.0001) break;
					}
				}
				
				if (1.0 - alpha < 0.0001) break;
			}
				
			if (1.0 - alpha < 0.0001) break;
		}
	}
	COLOR = blend(vec4(outline_color.rgb, alpha * outline_color.a), texture_color);
}
